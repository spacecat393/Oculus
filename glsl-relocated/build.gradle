plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation(shadow("io.github.douira:glsl-transformer:2.0.0-pre13")) {
        exclude module: "antlr4" // we only want to shadow the runtime module
    }
    implementation shadow("org.antlr:antlr4-runtime:4.11.1")
}

shadowJar {
    configurations = [project.configurations.shadow]

    from jar.archiveFile

    relocate 'org.antlr', 'oculus.org.antlr'

    archiveClassifier.set "shadow"
}



configurations {
    bundledJar {
        canBeConsumed = true
        canBeResolved = false
        // If you want this configuration to share the same dependencies, otherwise omit this line
        extendsFrom implementation, runtimeOnly
    }
}



artifacts {
    bundledJar(shadowJar)
}